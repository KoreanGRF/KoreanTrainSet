/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * ITX-Saemaeul
 */
SPEED_GENERAL(ITX_SAEMAEUL)
COST(ITX_SAEMAEUL)
RUNNING(ITX_SAEMAEUL)
CAPACITY(ITX_SAEMAEUL)
CAPACITY_PURCHASE(ITX_SAEMAEUL)
LOADING(ITX_SAEMAEUL)
// SPEED_GENERAL(ITX_SAEMAEUL_wagon)
// COST(ITX_SAEMAEUL_wagon)
// RUNNING(ITX_SAEMAEUL_wagon)
CAPACITY(ITX_SAEMAEUL_wagon)
CAPACITY_PURCHASE(ITX_SAEMAEUL_wagon)
// LOADING(ITX_SAEMAEUL_wagon)

// Graphic
spriteset(set_ITX_SAEMAEUL_purchase, "./src/locomotive/saemaeul/ITX-saemaeul.png") {
    tmpl_purchase_for_dualhead(0, 0)
}
spriteset(set_ITX_SAEMAEUL_engineH, "./src/locomotive/saemaeul/ITX-saemaeul.png") {
    [60,    0,     8,    21,     -3,     -14]
    [75,    0,    21,    15,    -14,      -6]
    [105,   0,    32,    12,    -16,      -8]
    [155,   0,    20,    16,     -6,      -8]
    [185,   0,     8,    20,     -3,     -13]
    [200,   0,    20,    16,    -14,      -7]
    [235,   0,    32,    12,    -15,      -8]
    [285,   0,    21,    15,     -7,      -7]
}
spriteset(set_ITX_SAEMAEUL_wagonH, "./src/locomotive/saemaeul/ITX-saemaeul.png") {
    [60,    30,     8,    22,     -3,    -14]
    [75,    30,    20,    16,    -14,     -7]
    [105,   30,    31,    12,    -15,     -8]
    [155,   30,    20,    16,     -6,     -8]
    [185,   30,     8,    21,     -3,    -13]
    [200,   30,    20,    16,    -14,     -7]
    [235,   30,    31,    12,    -15,     -8]
    [285,   30,    20,    16,     -6,     -8]
}
spriteset(set_ITX_SAEMAEUL_wagon, "./src/locomotive/saemaeul/ITX-saemaeul.png") {
    [60,    60,     8,    21,     -3,    -13]
    [75,    60,    20,    16,    -14,     -7]
    [105,   60,    31,    12,    -15,     -8]
    [155,   60,    20,    16,     -6,     -8]
}
spriteset(set_ITX_SAEMAEUL_wagonT, "./src/locomotive/saemaeul/ITX-saemaeul.png") {
    [60,    90,     8,    21,     -3,    -13]
    [75,    90,    20,    16,    -14,     -7]
    [105,   90,    31,    12,    -15,     -8]
    [155,   90,    20,    16,     -6,     -8]
    [185,   90,     8,    22,     -3,    -13]
    [200,   90,    20,    16,    -14,     -7]
    [235,   90,    31,    12,    -15,     -8]
    [285,   90,    20,    16,     -6,     -8]
}
spriteset(set_ITX_SAEMAEUL_engineT, "./src/locomotive/saemaeul/ITX-saemaeul.png") {
    [185,   120,     8,    21,     -3,   -13]
    [200,   120,    21,    15,    -14,    -6]
    [235,   120,    32,    12,    -16,    -8]
    [285,   120,    20,    16,     -6,    -8]
    [60,    120,     8,    20,     -3,   -12]
    [75,    120,    20,    16,    -14,    -7]
    [105,   120,    32,    12,    -15,    -8]
    [155,   120,    21,    15,     -7,    -7]
}

switch(FEAT_TRAINS, SELF, sw_ITX_SAEMAEUL_engine, position_in_consist_from_end == 0 && num_vehs_in_consist > 1) {
    1:    set_ITX_SAEMAEUL_engineT;
    set_ITX_SAEMAEUL_engineH;
}

// Wagon
switch(FEAT_TRAINS, SELF, sw_ITX_SAEMAEUL_wagon_by_free_tail, position_in_consist_from_end) {
    1: set_ITX_SAEMAEUL_wagonT;
    set_ITX_SAEMAEUL_wagon;
}
switch(FEAT_TRAINS, SELF, sw_ITX_SAEMAEUL_wagon, position_in_consist) {
    1: set_ITX_SAEMAEUL_wagonH;
    sw_ITX_SAEMAEUL_wagon_by_free_tail;
}

// Attachable vehicles
switch(FEAT_TRAINS, SELF, sw_ITX_SAEMAEUL_attach, vehicle_type_id) {
    ko_train_ITX_SAEMAEUL:  return CB_RESULT_ATTACH_ALLOW;
    ko_train_PASSWG:  return CB_RESULT_ATTACH_ALLOW;
    ko_train_PASSWGP: return CB_RESULT_ATTACH_ALLOW;
    return string(STR_ERROR_NO_ATTACH);
}

item(FEAT_TRAINS, ko_train_ITX_SAEMAEUL, 5540) {
    property {
        // Common properties
        name:                           string(STR_ITX_SAEMAEUL_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_ITX_SAEMAEUL_INTRODUCTION;
        track_type:                     ELRL;
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        dual_headed:                    1;

        // Spec
        power:                          POWER_IN_KW(var_ITX_SAEMAEUL_POWER);
        weight:                         20 ton;
        tractive_effort_coefficient:    0.25;
        air_drag_coefficient:           0.1;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 1;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];

        // Costs
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_ELECTRIC;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                        sw_ITX_SAEMAEUL_engine;
        purchase:                       set_ITX_SAEMAEUL_purchase;

        speed:                          sw_ITX_SAEMAEUL_speed;
        cost_factor:                    sw_ITX_SAEMAEUL_cost;
        running_cost_factor:            sw_ITX_SAEMAEUL_runningcost;
        cargo_capacity:                 sw_ITX_SAEMAEUL_capacity;
        purchase_cargo_capacity:        sw_ITX_SAEMAEUL_purchase_capacity;
        loading_speed:                  sw_ITX_SAEMAEUL_loadingspeed;

        can_attach_wagon:               sw_ITX_SAEMAEUL_attach;
        start_stop:                     sw_ko_train_start_stop;
        additional_text:                string(STR_REFIT_ADDITIONAL_UNABLE);
    }
    livery_override(ko_train_PASSWG) {
        default:                        sw_ITX_SAEMAEUL_wagon;
        cargo_capacity:                 sw_ITX_SAEMAEUL_wagon_capacity;
        purchase_cargo_capacity:        sw_ITX_SAEMAEUL_wagon_purchase_capacity;
        loading_speed:                  sw_ITX_SAEMAEUL_loadingspeed;
    }
    livery_override(ko_train_PASSWGP) {
        default:                        sw_ITX_SAEMAEUL_wagon;
        cargo_capacity:                 sw_ITX_SAEMAEUL_wagon_capacity;
        purchase_cargo_capacity:        sw_ITX_SAEMAEUL_wagon_purchase_capacity;
        loading_speed:                  sw_ITX_SAEMAEUL_loadingspeed;
        power:                          POWER_IN_KW(var_ITX_SAEMAEUL_wagon_POWER);
    }
}
// End of "ITX-Saemaeul"
