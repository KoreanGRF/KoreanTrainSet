/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * K8x00
 */
SPEED_GENERAL(K8x00)
COST(K8x00)
RUNNING(K8x00)

// Red & White & Blue (current)
spriteset(set_K8x00_RED_WHITE_BLUE_purchase, "./src/locomotive/K8x00/K8x00.png") {
    tmpl_purchase_for_singlehead(0, 0)
}
spriteset(set_K8x00_RED_WHITE_BLUE, "./src/locomotive/K8x00/K8x00.png") {
    tmpl_loco(60, 0)
}

// Green & Yellow
spriteset(set_K8x00_GREEN_YELLOW_purchase, "./src/locomotive/K8x00/K8x00.png") {
    tmpl_purchase_for_singlehead(0, 30)
}
spriteset(set_K8x00_GREEN_YELLOW, "./src/locomotive/K8x00/K8x00.png") {
    tmpl_loco(60, 30)
}

// Alias of purchase image
switch(FEAT_TRAINS, SELF, sw_K8x00_purchase, cargo_subtype) {
    set_K8x00_RED_WHITE_BLUE_purchase;
}

// Refit Paintings
switch(FEAT_TRAINS, SELF, sw_K8x00_loco, cargo_subtype) {
    1:    set_K8x00_GREEN_YELLOW;
    set_K8x00_RED_WHITE_BLUE;
}
switch(FEAT_TRAINS, SELF, sw_K8x00_cargo_subtype_text, cargo_subtype) {
    1:    return string(STR_REFIT_LIVERY_GREEN_YELLOW);
    return string(STR_REFIT_LIVERY_THESEDAYS);
}

// Item
item(FEAT_TRAINS, ko_train_K8x00, 5481) {
    property {
        // Common properties
        name:                           string(STR_K8x00_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_K8x00_INTRODUCTION;
        track_type:                     ELRL;
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;

        // Spec
        speed:                          150 km/h;
        power:                          POWER_IN_KW(var_K8x00_POWER);
        weight:                         var_K8x00_WEIGHT;
        tractive_effort_coefficient:    0.25;
        air_drag_coefficient:           0.01;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 1;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];

        // Costs
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_ELECTRIC;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                        sw_K8x00_loco;
        purchase:                       sw_K8x00_purchase;

        speed:                          sw_K8x00_speed;
        cost_factor:                    sw_K8x00_cost;
        running_cost_factor:            sw_K8x00_runningcost;
        cargo_capacity:                 0;

        can_attach_wagon:               sw_Kxxxx_attach;
        cargo_subtype_text:             sw_K8x00_cargo_subtype_text;
        additional_text:                string(STR_REFIT_ADDITIONAL_ABLE);
    }
}
// End of "K8x00"
