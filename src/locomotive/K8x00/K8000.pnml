/*------------------------------------------------------------------------------
  한국 열차 세트(Korean Train Set)
  * Official download site : https://telk.kr/ottd/newgrf/ko_train_set
  * Github repository      : https://github.com/KoreanGRF/KoreanTrainSet
------------------------------------------------------------------------------*/
/**
 * K8000
 */
SPEED_GENERAL(K8000)
COST(K8000)
RUNNING(K8000)

// Blue & White
spriteset(set_K8000_BLUE_WHITE_purchase, "./src/locomotive/K8x00/K8000.png") {
    tmpl_purchase_common(0, 0)
}
spriteset(set_K8000_BLUE_WHITE, "./src/locomotive/K8x00/K8000.png") {
    tmpl_loco(80, 0)
}

// Green & Yellow
spriteset(set_K8000_GREEN_YELLOW_purchase, "./src/locomotive/K8x00/K8000.png") {
    tmpl_purchase_common(0, 30)
}
spriteset(set_K8000_GREEN_YELLOW, "./src/locomotive/K8x00/K8000.png") {
    tmpl_loco(80, 30)
}

// Red & White & Blue
spriteset(set_K8000_RED_WHITE_BLUE_purchase, "./src/locomotive/K8x00/K8000.png") {
    tmpl_purchase_common(0, 60)
}
spriteset(set_K8000_RED_WHITE_BLUE, "./src/locomotive/K8x00/K8000.png") {
    tmpl_loco(80, 60)
}

// Alias of purchase image
switch(FEAT_TRAINS, SELF, sw_K8000_purchase, cargo_subtype) {
    set_K8000_RED_WHITE_BLUE_purchase;
}


// Switch
switch(FEAT_TRAINS, SELF, sw_K8000, cargo_subtype) {
    2:    set_K8000_GREEN_YELLOW;
    1:    set_K8000_BLUE_WHITE;
    set_K8000_RED_WHITE_BLUE;
}
switch(FEAT_TRAINS, SELF, sw_K8000_cargo_subtype_text, cargo_subtype) {
    1:    return string(STR_REFIT_LIVERY_MUGUNGHWA_1970_1983);
    2:    return string(STR_REFIT_LIVERY_GREEN_YELLOW);
    return string(STR_REFIT_LIVERY_THESEDAYS);
}

// Item
item(FEAT_TRAINS, ko_train_K8000, 5480) {
    property {
        // Common properties
        name:                           string(STR_K8000_NAME);
        climates_available:             ALL_CLIMATES;
        introduction_date:              var_K8000_INTRODUCTION;
        track_type:                     ELRL;
        engine_class:                   ENGINE_CLASS_ELECTRIC;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;

        // Spec
        speed:                          85 km/h;
        power:                          POWER_IN_KW(var_K8000_POWER);
        weight:                         var_K8000_WEIGHT;
        tractive_effort_coefficient:    0.25;
        air_drag_coefficient:           0.01;

        // Model
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        retire_early:                   0;
        reliability_decay:              0;

        // Cargo
        cargo_capacity:                 1;
        refittable_cargo_classes:       NO_CARGO_CLASS;
        non_refittable_cargo_classes:   NO_CARGO_CLASS;
        cargo_allow_refit:              [PASS];
        cargo_disallow_refit:           [];

        // Costs
        refit_cost:                     0;
        running_cost_base:              RUNNING_COST_ELECTRIC;

        // Flags
        ai_special_flag:                AI_FLAG_PASSENGER;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT, TRAIN_FLAG_FLIP);
        bitmask_vehicle_info:           0;
    }
    graphics {
        default:                        sw_K8000;
        purchase:                       sw_K8000_purchase;

        speed:                          sw_K8000_speed;
        cost_factor:                    sw_K8000_cost;
        running_cost_factor:            sw_K8000_runningcost;
        cargo_capacity:                 0;

        can_attach_wagon:               sw_Kxxxx_attach;
        cargo_subtype_text:             sw_K8000_cargo_subtype_text;
        additional_text:                string(STR_REFIT_ADDITIONAL_ABLE);
    }
}
// End of "K8000"
